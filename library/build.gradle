plugins {
    // Apply the java-library plugin for API and implementation separation.
    id("java-library")
    id("io.micronaut.library") version "4.5.3"
    id("idea")
    id("maven-publish")
}

version = "1.0.0"
group = "io.github.baylorpaul.webauthn4jmicronaut"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.validation:micronaut-validation")
    compileOnly("org.projectlombok:lombok")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testAnnotationProcessor("org.projectlombok:lombok")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:testcontainers")
    testCompileOnly("org.projectlombok:lombok")
}


// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    // generate a sources JAR for the library
    withSourcesJar()
    // generate a Javadoc JAR for the library
    withJavadocJar()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}


micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("io.github.baylorpaul.webauthn4jmicronaut.*")
    }
}


// Publish via GitHub Packages with e.g. GITHUB_ACTOR=myuser GITHUB_TOKEN=myPersonalAccessToken ./gradlew publish
publishing {
    repositories {
        //mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/baylorpaul/webauthn4j-micronaut")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        //maven(MavenPublication) {
        //    groupId = project.group
        //    artifactId = project.name
        //    version = project.version
        //    from components.java
        //}
        gpr(MavenPublication) {
            from(components.java)

            pom {
                //name = "${project.group}:${project.name}"
                name = 'WebAuthn4J Micronaut library'
                description = 'A WebAuthn/Passkeys library for Micronaut via WebAuthn4J, with a sample app using PostgreSQL persistence.'
                url = 'https://github.com/baylorpaul/webauthn4j-micronaut'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/baylorpaul/webauthn4j-micronaut/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'baylorpaul'
                        name = 'Paul Poley'
                        //email = '141367555+baylorpaul@users.noreply.github.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/baylorpaul/webauthn4j-micronaut.git'
                    developerConnection = 'scm:git:ssh://github.com:baylorpaul/webauthn4j-micronaut.git'
                    url = 'https://github.com/baylorpaul/webauthn4j-micronaut'
                }
            }
        }
    }
}
